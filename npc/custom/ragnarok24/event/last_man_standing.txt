prontera,187,141,5	script	ПВП Эвент	4_M_THAIONGBAK,{
	if ( !.start ) {
		mes "Эвент Закрыт";
		close;
	}
	if ( .start == 2 ) {
		mes "Эвент Начат";
		close;
	}
	if ( .register_count >= .register_limit ) {
		mes "Эвент достиг максимум играков!";
		close;
	}
	percentheal 100,100;
	warp "guild_vs5", 0,0;
	.register_aid[ .register_count ] = getcharid(3);
	.register_count++;
	end;
OnWhisperGlobal:
	if ( getgmlevel() < 60 ) end;
OnClock1600: // put all your start timer here
//OnMinute00:
	if ( .start == 2 )
		callsub L_resetmap;
	else if ( .start == 1 )
		end;
	announce "Регистрация ПВП Эвента открыта 3 минуты", 0;
	.start = 1;
	sleep 180000; // registration timer here
	announce "Регистрация пвп эвента закрыта!", 0;
	.start = 2;
	sleep 3000;
	mapannounce "guild_vs5", "Начали !!!!!", 0;
	if ( .register_count < .register_min ) {
		announce "Набралось не достаточно героев!", 0;
		mapwarp "guild_vs5", .map$, .x, .y;
		callsub L_resetmap;
		end;
	}
	pvpon "guild_vs5";
	end;
OnPCDieEvent:
OnPCLogoutEvent:
	if ( .start != 2 || strcharinfo(3) != "guild_vs5" ) end;
	while ( .register_aid != getcharid(3) && .@i < .register_count ) .@i++;
	deletearray .register_aid[.@i], 1;
	.register_count--;
	warp "SavePoint", 0,0;
	if ( .register_count > 1 ) end;
	killmonsterall "guild_vs5";
	announce "Поздравляем ~ Победитель Гладиатор арены "+ rid2name( .register_aid ), 0;
	getitem .reward_item_id, .reward_item_amount, .register_aid; // winner prize
	warpchar "SavePoint", 0,0, getcharid( 0, rid2name( .register_aid ) );
	callsub L_reset;
	end;
L_resetmap:
	mapwarp "guild_vs5", .map$, .x, .y;
L_reset:
	.start = 0;
	deletearray .register_aid;
	.register_count = 0;
	pvpoff "guild_vs5";
	return;
OnInit:
	getmapxy .map$, .x, .y, 1;
	.register_min = 2; // minimum amount of players to start this event, or else it auto-abort
	.register_limit = 100; // maximum amount of players able to participate in this event
	.reward_item_id = 7227;
	.reward_item_amount = 10;
	end;
}
guild_vs5	mapflag	nosave	SavePoint
guild_vs5	mapflag	nowarp
guild_vs5	mapflag	nowarpto
guild_vs5	mapflag	noteleport
guild_vs5	mapflag	nomemo
guild_vs5	mapflag	nopenalty
//guild_vs5	mapflag	nobranch
guild_vs5	mapflag	noicewall
guild_vs5	mapflag	pvp_noparty
guild_vs5	mapflag	pvp_noguild