//===== Hercules Script ======================================
//= Find the Mushroom
//===== By: ==================================================
//= Mysterious
//===== Current Version: =====================================
//= 3.6a
//===== Description: =========================================
//= Find the Mushroom - random amount of Mushrooms spawns in random maps.
//= Players need to find these mushrooms and kill them to gain prizes!
//===== Additional Comments: =================================
//= 3.0 Fully Functional with Rewritten script. [Mysterious]
//= 3.6a Slightly edited. [Euphy]
//============================================================

prontera,142,227,6	script	Find the Mushroom	BLACK_MUSHROOM,{
	mes "[ Find The Mushroom ]";
	if (!.status)
		mes "There is no event at the moment!";
	else {
		mes "There are "+.Spawn+" Mushrooms left in "+.map$+"!";
		mes "Найти и убить грибы, чтобы получить "+getitemname(.Prize)+"!";
	}
	if (.status || getgmlevel() < .GM) close;
	mes "Start the event?";
	next;
	if(select("- No", "- Yes") == 1) close;
	donpcevent strnpcinfo(0)+"::OnMinute10";
	mes "[ Find The Mushroom ]";
	mes "Event started!";
	close;

OnInit:
	set .Prize,22777;	// Reward item ID
	set .Amount,1;	// Reward item amount
	set .GM,60;	// GM level required to access NPC
	setarray .maps$[0],"prontera","prontera","prontera","prontera"; // Possible maps
	end;

OnMinute15:	// Start time (every hour)
	if (.status) end;
	set .status,1;
	set .Spawn,rand(10,20);	// How many Mushrooms should spawn?
	set .map$,.maps$[rand(getarraysize(.maps$))];
	killmonster .map$,"All";
	monster .map$,0,0,"Please don't kill me!",1084,.Spawn,strnpcinfo(0)+"::OnMobKilled";
	announce "Найти гриб : Total of "+.Spawn+" Грибы были замечены в "+.map$+"!",0;
	sleep 2500;
	announce "Найти гриб : Каждый гриб что вы убиваете даст вам "+getitemname(.Prize)+"!",0;
	end;

OnMobKilled:
	set .Spawn, .Spawn - 1;
	getitem .Prize, .Amount;
	if (.Spawn) announce "[ "+strcharinfo(0)+" ] убил гриб. There are now "+.Spawn+" Mushroom(s) left.",bc_map;
	else {
		announce "Найти Гриб событие закончилось. Все грибы были убиты.",0;
		set .status,0;
	}
	end;
}
