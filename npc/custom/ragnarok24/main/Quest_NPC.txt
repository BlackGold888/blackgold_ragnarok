// ============== Shatowolf Gaming =========================
//============================================================
sky_quest,38,54,3	script	Lard	732,{ callfunc "qshop",1;
	OnInit:
	waitingroom "Costume Item Quest",0;
	end;
	}

sky_quest,42,54,3	script	Aiai	772,{ callfunc "qshop",2;
	OnInit:
	waitingroom "Headgear Quest",0;
	end;
	}
	
sky_quest,49,54,3	script	Minsyu	964,{ callfunc "qshop",6;
	OnInit:
	waitingroom "Midgear Quest",0;
	end;
	}	
sky_quest,53,54,3	script	Aim	953,{ callfunc "qshop",4;
	OnInit:
	waitingroom "Weapon Quest",0;
	end;
	}	

sky_quest,54,49,3	script	Marl	794,{ callfunc "qshop",5;
	OnInit:
	waitingroom "Aura/Wings Quest",0;
	end;
	}

sky_quest,46,55,3	script	Saldie	503,{ callfunc "qshop",3;
	OnInit:
	waitingroom "Lower Headgear Quest",0;
	end;
	}

market,96,120,3	script	Anto	56,{ callfunc "qshop",7;
	OnInit:
	waitingroom "Advance Item Shop",0;
	end;
	}

market,107,115,3	script	Anne	814,{ callfunc "qshop",8;
	OnInit:
	waitingroom "Yggdrasil Shop",0;
	end;
	}

market,89,108,3	script	Arturo	419,{ callfunc "qshop",9;
	OnInit:
	waitingroom "Item Set Shop",0;
	end;
	}

sky_quest,37,49,3	script	Brent	405,{ callfunc "qshop",10;
	OnInit:
	waitingroom "Rare Quest",0;
	end;
	}

// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Costume Item Quest","Quest2","Quest3","Quest4","Quest5","Quest6","Quest7","Quest8","Quest9","Quest10";


// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------


	Add(1,3042,1,0,0,3072,1,671,50);
	Add(1,3043,1,0,0,671,25);


	Add(1,3072,1,0,0,7122,35,7097,30,1035,20,671,5);
	Add(1,3067,1,0,0,7011,35,923,1,1041,20,671,2);
	Add(1,3068,1,0,0,7011,35,923,1,1041,20,671,2);
	Add(1,3069,1,0,0,7011,35,923,1,1041,20,671,2);
	Add(1,3070,1,0,0,7011,35,923,1,1041,20,671,2);
	Add(1,3071,1,0,0,7011,35,923,1,1041,20,671,2);

	Add(1,3059,1,0,0,7007,35,951,15,1018,20,671,2);
	Add(1,3060,1,0,0,7007,35,951,15,1018,20,671,2);
	Add(1,3061,1,0,0,7007,35,951,15,1018,20,671,2);
	Add(1,3062,1,0,0,7007,35,951,15,1018,20,671,2);
	Add(1,3063,1,0,0,7007,35,951,15,1018,20,671,2);
	
	Add(1,3064,1,0,0,949,40,928,15,924,25,671,2);
	Add(1,3065,1,0,0,949,40,928,15,924,25,671,2);
	Add(1,3066,1,0,0,949,40,928,15,924,25,671,2);
	Add(1,3050,1,0,0,949,40,928,15,924,25,671,2);
	Add(1,3051,1,0,0,949,40,928,15,924,25,671,2);
	
	Add(1,3052,1,0,0,7096,5,1038,1,911,40,671,2);
	Add(1,3053,1,0,0,7096,5,1038,1,911,40,671,2);
	Add(1,3054,1,0,0,7096,5,1038,1,911,40,671,2);
	Add(1,3055,1,0,0,7096,5,1038,1,911,40,671,2);
	Add(1,3056,1,0,0,7096,5,1038,1,911,40,671,2);
	
	Add(1,3057,1,0,0,1022,1,960,25,7072,15,671,2);
	Add(1,3058,1,0,0,1022,1,960,25,7072,15,671,2);
	
	
	Add(2,3000,1,0,0,4110,15,671,5);
	Add(2,3001,1,0,0,4343,15,671,5);
	Add(2,3002,1,0,0,4269,15,671,5);
	Add(2,3003,1,0,0,4412,15,671,5);
	Add(2,3005,1,0,0,4434,15,671,5);
	Add(2,3006,1,0,0,4195,15,671,5);
	Add(2,3008,1,0,0,4112,15,671,5);
	Add(2,3011,1,0,0,4206,15,671,5);

	Add(2,3015,1,0,0,4127,15,671,5);
	Add(2,3017,1,0,0,4311,15,671,5);
	Add(2,3018,1,0,0,4185,15,671,5);




	Add(3,3004,1,0,0,671,10);
	Add(3,3007,1,0,0,671,10);
	Add(3,3016,1,0,0,671,10);
	Add(3,3014,1,0,0,671,10);
	Add(3,3019,1,0,0,671,10);
	
	Add(6,3009,1,0,0,4215,5,4178,2,671,3);
	Add(6,3010,1,0,0,4293,5,4056,5,671,3);

	Add(6,3013,1,0,0,4040,5,4088,5,671,3);
	Add(6,3021,1,0,0,4084,5,4081,5,671,3);
	Add(6,3020,1,0,0,4015,5,4287,3,671,3);


	//Add(4,1690,1,0,0,4020,20,671,35);
	Add(4,1295,1,0,0,4104,20,671,37);
	Add(4,18400,1,0,0,4029,15,671,25);
	Add(4,13491,1,0,0,4096,30,671,35);	

	Add(4,1228,1,0,0,4114,5,671,5);
	Add(4,13412,1,0,0,4410,5,671,5);
	Add(4,13413,1,0,0,4410,5,671,5);
	Add(4,2000,1,0,0,4405,3,671,2);

	Add(5,3090,1,0,0,671,15);

	Add(5,3091,1,0,0,671,15);
	Add(5,3092,1,0,0,671,15);
	Add(5,3093,1,0,0,671,15);
	Add(5,3094,1,0,0,671,15);
	Add(5,3095,1,0,0,671,15);
	Add(5,3096,1,0,0,671,15);
	Add(5,3097,1,0,0,671,15);
	Add(5,3098,1,0,0,671,15);
	Add(5,3099,1,0,0,671,15);


	Add(7,7776,1,0,0,671,3);
	Add(7,5374,1,0,0,671,10);
	Add(7,5518,1,0,0,671,10);

	Add(7,5363,1,0,0,671,5);
	Add(7,5383,1,0,0,671,5);
	Add(7,5360,1,0,0,671,5);
	Add(7,5423,1,0,0,671,5);
	Add(7,5495,1,0,0,671,5);

	Add(7,5381,1,0,0,671,5);
	Add(7,5359,1,0,0,671,5);
	Add(7,5342,1,0,0,671,5);
	Add(7,5343,1,0,0,671,5);
	Add(7,5123,1,0,0,671,5);
	Add(7,5132,1,0,0,671,5);





	Add(7,5800,1,0,0,671,2);
	Add(7,5389,1,0,0,671,2);
	Add(7,5421,1,0,0,671,2);
	Add(7,5471,1,0,0,671,2);
	Add(7,5325,1,0,0,671,2);





	Add(7,5596,1,0,0,671,2);

	Add(7,5775,1,0,0,671,2);
	Add(7,5155,1,0,0,671,2);
	Add(7,5156,1,0,0,671,2);
	Add(7,5403,1,0,0,671,2);
	Add(7,5361,1,0,0,671,2);
	Add(7,5377,1,0,0,671,5);
	Add(7,5548,1,0,0,671,2);

	Add(8,14232,1,0,0,969,5);
	Add(8,12534,1,0,0,969,3);

	Add(9,2375,1,0,0,671,5);
	Add(9,2433,1,0,0,671,5);
	Add(9,2537,1,0,0,671,2);
	Add(9,2374,1,0,0,671,5);
	Add(9,2729,1,0,0,671,1);

	Add(9,2357,1,0,0,671,3);
	Add(9,2524,1,0,0,671,3);
	Add(9,2421,1,0,0,671,3);
	Add(9,2115,1,0,0,671,3);

	Add(10,2554,1,0,0,4425,1,4168,1,4134,1,4143,1,671,100);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
-	shop	qshop7	-1,909:-1
-	shop	qshop8	-1,909:-1
-	shop	qshop9	-1,909:-1
-	shop	qshop10	-1,909:-1