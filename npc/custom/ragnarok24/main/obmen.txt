//===== eAthena Script =======================================
// Обменник MvP карт
//===== Автор ================================================
// nomn. ICQ 898022
//===== Текущая версия =======================================
// 1.0
//===== Совместимость ========================================
// All eA
//===== Описание =============================================
// Меняет 3 любых МвП карты на одну из заданных МвП карт (рандомно)
//===== Изменения ============================================
// v1.0 - Релиз.

prontera,127,152,4	script	Обмен MVP Карт	4_M_YOUNGKNIGHT,{

        mes "[Обмен Карт]";
        
        if (mvp_exchange > 2)
        {
                mes "Готов получить свою карту?";
                
                if (select("Да","Нет") == 2)
                {
                        mes "Ок. Приходи как надумаешь.";
                        close;
                }
                
                next;
                mes "[Обмен Карт]";
                set @tmp_card, $mvp_cards[rand(getarraysize($mvp_cards))];
                mes "Ты получаешь:";
                mes "^A512E3"+getitemname(@tmp_card)+"^000000";
                getitem @tmp_card, 1;
                set mvp_exchange, 0;
                close;
        }
        
        mes "Привет, я обмениваю MvP карты.";
        
        if (mvp_exchange > 0)
        {
                mes "Сейчас у меня ^A512E3"+mvp_exchange+" из 3^000000 твоих карт.";
        }
        
        mes "Чем могу помочь?";
        switch(select("Обменять","^A512E3Информация^000000","Ничем")){
        
                case 1:
                        next;
                        mes "[Обмен Карт]";
                        mes "За 3 твоих MvP карты я выдам тебе какую-нибудь одну, из списка.";
                        mes "^A512E3Какую именно карту тебе дать выбираю я сам.^000000";
                        mes "Согласен?";
                        
                        if (select("Нет","Да") == 1)
                                close;

                        next;
                
                        for(set .@i,0; .@i < getarraysize($mvp_cards); set .@i, .@i+1)
                        {
                                if (countitem($mvp_cards[.@i]) > 0)
                                {
                                        mes "[Обмен Карт]";
                                        mes "Я нашел у тебя 1 ^A512E3"+getitemname($mvp_cards[.@i])+"^000000";
                                        mes "Засчитать?";
                                        
                                        if (select("Нет","Да") == 2)
                                        {
                                                delitem $mvp_cards[.@i], 1;
                                                set mvp_exchange, mvp_exchange + 1;
                                                if (mvp_exchange == 3)
                                                {
                                                        mes "Достаточно. Секунду...";
                                                        close;
                                                        break;
                                                }
                                        }
                                        next;
                                }
                        }
                        mes "[Обмен Карт]";
                        mes "MvP Карт не найдено!";
                        close;
                break;
                
                case 2:
                        next;
                        mes "[Обмен Карт]";
                        mes "Процедура обмена: Я осматриваю твой инвентарь и выбираю из него MvP карты, поодной.";
                        mes "Пока ты не принесешь мне ^A512E33 карты я не продолжу обмен.^000000";
                        mes "Т.е. можешь принести мне первую сегодня, вторую послезавтра, а третью хоть через неделю.";
                        mes "Как только все 3 карты у меня я выдаю тебе в замен одну другую. При этом она может совпасть с одной из тех, что ты мне сдал.";
                        if (select("Список возможных карт","Ясно. Пока.") == 2)
                                close;
                
                        next;
                        mes "[Обмен Карт]";
                        for(set .@i,0; .@i < getarraysize($mvp_cards); set .@i, .@i+1)
                                mes getitemname($mvp_cards[.@i]);
                                
                        close;
                break;
                
                case 3:
                        close;
                break;
        }
        end;
        
OnInit:
        setarray $mvp_cards[0], 4144,4147,4142,4132,4128,4143,4137,4123,4146,4131,4148,
        4121,4135,4318,4324,4168,4305,4276,4241,4134,4330,4263,4236,4302,4342,4372,4352,
        4374,4376,4386,4403,4407,4408,4419,4425,4430,4441,4359,4363,4365,4357,4361;
        end;
}